import org.gradle.api.Plugin
import org.gradle.api.Project
import ru.vyarus.gradle.plugin.python.task.PythonTask;

class AirbytePythonPoetryConfiguration {
    String moduleDirectory
}

class AirbytePythonPoetryPlugin implements Plugin<Project> {

    void apply(Project project) {
        def extension = project.extensions.create('airbytePython', AirbytePythonPoetryConfiguration)

        project.plugins.apply 'ru.vyarus.use-python'

        project.python {
            envPath = '.venv'
            minPythonVersion = '3.7'
            scope = 'VIRTUALENV'
            installVirtualenv = true
            pip 'flake8:3.8.4'
            pip 'black:20.8b1'
            pip 'mypy:0.790'
            pip 'isort:5.6.4'
            pip 'pytest:6.1.2'
            pip 'pip:20.3'
            pip 'poetry:1.1.4'
            pip 'pex:2.1.21'
        }

        project.task('blackFormat', type: PythonTask) {
            module = "black"
            // the line length should match .isort.cfg
            command = ". --line-length 140"
            dependsOn project.rootProject.spotlessPythonApply
        }

        project.task('isortFormat', type: PythonTask) {
            module = "isort"
            command = ". --settings-file ${project.rootProject.file('tools/python/.isort.cfg').absolutePath}"
        }

        project.task('flakeCheck', type: PythonTask, dependsOn: project.blackFormat) {
            module = "flake8"
            command = ". --config ${project.rootProject.file('tools/python/.flake8').absolutePath}"
        }

        project.task('installReqs1', type: PythonTask) {
            module = "poetry"
            command = "install"
            inputs.file('pyproject.toml')

            // HACK: makes all integrations depend on installing requirements for bases. long term we should resolve deps and install in order.
            if(project.getPath().startsWith(":airbyte-integrations:connectors")) {
                dependsOn project.rootProject.getTasksByName("airbytePythonApply", true).findAll { it.project.getPath().startsWith(":airbyte-integrations:bases")}
            }
        }

        project.task('installReqs2', type: PythonTask, dependsOn: project.installReqs1) {
            module = "poetry"
            command = "run pip freeze > requirements.txt"
        }

        project.task('installReqs', type: PythonTask, dependsOn: project.installReqs2) {
            module = "pex"
            command = "--requirement=requirements.txt --sources-directory=. --output-file=service.pex"
        }

        if(project.file('unit_tests').exists()) {
            project.task('unitTest', type: PythonTask, dependsOn: project.installTestReqs) {
                module = "pip"
                command = "poetry run python -m pytest unit_tests"
            }
        } else {
            project.task('unitTest') {
                logger.info "Skipping Python unit tests because unit_tests directory doesn't exist."
            }
        }

        if (extension.moduleDirectory) {
            project.task('mypyCheck', type: PythonTask) {
                module = "mypy"
                command = "-m ${extension.moduleDirectory} --config-file ${project.rootProject.file('tools/python/.mypy.ini').absolutePath}"
            }

            project.check.dependsOn mypyCheck
        }

        project.task('airbytePythonApply', type: DefaultTask) {
            dependsOn project.installReqs
            dependsOn project.blackFormat
            dependsOn project.isortFormat
            dependsOn project.flakeCheck
        }


        project.task('airbytePythonTest', type: DefaultTask) {
            dependsOn project.airbytePythonApply
            dependsOn project.installReqs
            dependsOn project.unitTest
        }

        project.assemble.dependsOn project.airbytePythonApply
        project.assemble.dependsOn project.airbytePythonTest
        project.test.dependsOn project.airbytePythonTest
    }
}
